# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: naxuroqa <naxuroqa at gmail.com>

_pkgname=libtoxcore
pkgname="${_pkgname}-git"
pkgver=r2847.gb41eadf
pkgrel=1

pkgdesc='Peer to peer distributed multimedia messenger (core library)'
arch=('i686' 'x86_64')
url='https://tox.im/'
license=('GPL3')

depends=('libconfig>=1.4' 'libsodium>=1.0' 'libvpx' 'opus')
makedepends=('check' 'git')

provides=("${_pkgname}" 'tox' 'toxcore')
conflicts=("${_pkgname}" 'tox' 'toxcore')

source=("${pkgname}::git+https://github.com/irungentoo/toxcore")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  autoreconf -i
  ./configure --prefix=/usr \
              --enable-ntox \
              --enable-daemon
  make
}

# check() {
#   cd "${pkgname}"
#   make check
# }

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}

