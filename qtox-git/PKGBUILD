# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=qtox
pkgname="${_pkgname}-git"
pkgver=r1753.g9bc6ae5
pkgrel=1

pkgdesc='Qt Tox client'
arch=('i686' 'x86_64')
url='https://github.com/tux3/qTox'
license=('GPL3')

depends=('libtoxcore' 'qt5-base' 'desktop-file-utils' 'opencv' 'openal' 'libxss'
         'libfilteraudio')
makedepends=('git' 'qt5-tools')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

install="${pkgname}.install"

source=("${pkgname}::git+https://github.com/tux3/qTox.git"
        "${_pkgname}.desktop")
md5sums=('SKIP'
         '775f8d0403e28f674b160f6df10a5adb')
sha256sums=('SKIP'
            '681586914321da3131babc23702be7494d270bf7b635c78771399359ea497b63')

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  mkdir build && cd build
  qmake-qt5 ..
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 "build/${_pkgname}" \
                 "${pkgdir}/usr/bin/${_pkgname}"

  install -Dm644 "../${_pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${_pkgname}.desktop"

  install -Dm644 "img/icons/${_pkgname}.svg" \
                 "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${_pkgname}.png"

  for _icon in img/icons/*.png; do
    _size=$(egrep -o '[0-9]+x[0-9]+' <<< "${_icon}")
    install -Dm644 "${_icon}" \
                   "${pkgdir}/usr/share/icons/hicolor/${_size}/apps/${_pkgname}.png"
  done
}
