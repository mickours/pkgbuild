# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>

_pkgname=tox-weechat
pkgname="${_pkgname}-git"
pkgver=r165.5f3d2f0
pkgrel=1

pkgdesc='Tox protocol plugin for WeeChat'
arch=('i686' 'x86_64')
url='https://github.com/haavardp/tox-weechat'
license=('GPL3')

depends=('tox' 'weechat>=1.0.1' 'sqlite>=3.6.19')
makedepends=('git' 'cmake>=2.8.7')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("${pkgname}::git+https://github.com/haavardp/tox-weechat.git")
md5sums=('SKIP')

install="${_pkgname}.install"

pkgver() {
  cd "${pkgname}"
  printf 'r%s.g%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}/" install
}

