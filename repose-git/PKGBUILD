# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Simon Gomizelj <simongmzlj@gmail.com>

_pkgname=repose
pkgname="${_pkgname}-git"
pkgver=r422.gfcd8b49
pkgrel=1

pkgdesc='An Arch Linux repo poking tool'
arch=('i686' 'x86_64')
url='https://github.com/vodik/repose'
license=('GPL')

depends=('pacman')
makedepends=('git')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

source=("${pkgname}::git+https://github.com/vodik/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  echo "r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

