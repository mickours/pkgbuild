# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

_pkgname=dwm
pkgname="${_pkgname}-xft"
pkgver=6.0
pkgrel=1

pkgdesc='A dynamic window manager for X (with Xft support)'
arch=('i686' 'x86_64')
url='http://dwm.suckless.org'
license=('MIT')

depends=('libx11' 'libxinerama' 'libxft')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

install=dwm.install

source=("http://dl.suckless.org/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        'config.h'
        'dwm.desktop'
        "${pkgname}.patch")
md5sums=('8bb00d4142259beb11e13473b81c0857'
         '3673d73a7fddb9c7c8845a3293e4bc6a'
         '939f403a71b6e85261d09fc3412269ee'
         '386773a3099d54d8475f2d87d25f2360')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/${pkgname}.patch"
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  cp "${srcdir}/config.h" config.h
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/#CFLAGS += -g/g' config.mk
  sed -i 's/^#CFLAGS = -std/CFLAGS += -std/g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 "${srcdir}/dwm.desktop" "${pkgdir}/usr/share/xsessions/dwm.desktop"
}
