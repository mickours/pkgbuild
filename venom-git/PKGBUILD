# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: naxuroqa <naxuroqa@gmail.com>

_pkgname=venom
pkgname="${_pkgname}-git"
pkgver=619.86f56e4
pkgrel=1

pkgdesc='Vala/Gtk+ Tox client'
arch=('i686' 'x86_64')
url='https://github.com/naxuroqa/Venom'
license=('GPL')

depends=('tox' 'gtk3>=3.4.1' 'sqlite>=3.7.9' 'json-glib>=0.14.2'
         'qrencode>=3.1.1' 'libnotify>=0.7.3' 'desktop-file-utils')
makedepends=('git' 'cmake' 'vala>=0.18.1')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

install="${_pkgname}.install"

source=("${pkgname}::git+https://github.com/naxuroqa/Venom.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${pkgname}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_QR_ENCODE=ON \
        -DENABLE_LIBNOTIFY=ON \
        .
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}

