# Maintainer: HÃ¥vard Pettersson <mail@haavard.me>

pkgname=ratox
pkgver=0.1
pkgrel=1

pkgdesc='FIFO based Tox client'
arch=('i686' 'x86_64')
url='http://ratox.2f30.org'
license=('MIT')

depends=('tox')

source=("http://git.2f30.org/ratox/snapshot/ratox-${pkgver}.tar.gz"
        'tox_is_save_encrypted.patch')
md5sums=('c77bb09c0ddb1220d74de9396a3c5ecb'
         '1dec20c23ab25ec87aa556f69246e82e')
sha256sums=('34b8d064be1dc2414f3ee54710bec30325101c5828b2f9b7f437a9b921fef6a7'
            '3c67bea6e0dec6c2bcfe2960a010d6a3d8bb27b384f513f1b37886e35c48a551')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/tox_is_save_encrypted.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

